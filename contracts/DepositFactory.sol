// SPDX-License-Identifier: MIT
pragma solidity 0.8.17;

import {Create2} from "@openzeppelin/contracts/utils/Create2.sol";
import {Clones} from "@openzeppelin/contracts/proxy/Clones.sol";
import {DepositBase, DepositConfig, DepositQuote, IERC20, IFeePolicy, validateParties,
    InvalidConfiguration, UnauthorizedRelayer} from "./DepositBase.sol";

/// @dev Construction-only policy, in token base units and basis points. Bounds a charge, not actual gas spent.
struct TokenPolicy {
    IERC20 token;
    uint120 maxGasFee;
    uint16 maxGasFeeBps;
}

/// @notice Shared CREATE2 deployment and fee policy. No owner, setters, upgrades or withdrawal authority.
abstract contract DepositFactory is IFeePolicy {
    address public immutable pool;
    address public immutable implementation;
    struct GasPolicy {
        uint120 fixedAllowance;
        uint16 basisPoints;
        bool supported;
    }
    mapping(IERC20 => GasPolicy) public gasPolicies;

    error InvalidPool();
    error UnsupportedAsset();
    error InvalidAmount();
    event Deployed(address indexed deposit, bytes32 indexed salt);
    event TokenPolicySet(address indexed token, uint120 fixedAllowance, uint16 basisPoints);

    constructor(address target, TokenPolicy[] memory policies, bytes memory implementationCode) {
        if (target.code.length == 0) revert InvalidPool();
        if (policies.length == 0) revert InvalidConfiguration();
        pool = target;
        for (uint256 i; i < policies.length; ++i) {
            TokenPolicy memory policy = policies[i];
            if ((address(policy.token).code.length == 0 && !(address(policy.token) == address(0) && _supportsNative()))
                || gasPolicies[policy.token].supported
                || policy.maxGasFeeBps > 9_990) revert InvalidConfiguration();
            gasPolicies[policy.token] = GasPolicy(policy.maxGasFee, policy.maxGasFeeBps, true);
            emit TokenPolicySet(address(policy.token), policy.maxGasFee, policy.maxGasFeeBps);
        }
        // One locked implementation per factory, independently derivable by the recovery tool.
        implementation = Create2.deploy(0, bytes32(0), abi.encodePacked(implementationCode, abi.encode(target)));
    }

    function maxGasFee(IERC20 token, uint256 amount) external view returns (uint256) {
        GasPolicy memory policy = gasPolicies[token];
        if (!policy.supported) revert UnsupportedAsset();
        if (amount == 0 || amount > type(uint120).max) revert InvalidAmount();
        return uint256(policy.fixedAllowance) + amount * policy.basisPoints / 10_000;
    }

    function computeAddress(bytes32 salt, DepositConfig calldata config) public view returns (address predicted) {
        validateParties(config);
        _validateRecipient(config.recipient);
        predicted = Clones.predictDeterministicAddress(implementation, _salt(salt, config));
        if (config.recovery == predicted || config.feeRecipient == predicted) revert InvalidConfiguration();
    }

    /// @notice Permissionless deployment for recovery. Existing deployments are never reconfigured.
    function deploy(bytes32 salt, DepositConfig calldata config) public returns (DepositBase forwarder) {
        address predicted = computeAddress(salt, config);
        if (predicted.code.length != 0) return DepositBase(predicted);
        forwarder = DepositBase(Clones.cloneDeterministic(implementation, _salt(salt, config)));
        forwarder.initialize(config);
        emit Deployed(address(forwarder), salt);
    }

    function deployAndExecute(bytes32 salt, DepositConfig calldata config, DepositQuote calldata quote, bytes calldata data)
        external returns (address)
    {
        if (msg.sender != config.relayer) revert UnauthorizedRelayer();
        DepositBase forwarder = deploy(salt, config);
        forwarder.execute(quote, data);
        return address(forwarder);
    }

    // Binding all initialization data prevents the same address being claimed with different terms.
    function _salt(bytes32 salt, DepositConfig calldata config) private pure returns (bytes32) {
        return keccak256(abi.encode(salt, config));
    }

    function _validateRecipient(bytes calldata recipient) internal pure virtual;

    function _supportsNative() internal pure virtual returns (bool) { return false; }
}
